import java.time.Instant
import java.time.ZoneId
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
}

group 'com.esaulpaugh'
version '1.3-SNAPSHOT'
archivesBaseName = "headlong-cli"

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    if (JavaVersion.current() >= JavaVersion.VERSION_1_10) {
        options.compilerArgs.addAll(['--release', '8'])
    }
    options.encoding = "UTF-8"
}

repositories {
    mavenCentral()
//    mavenLocal()
}

final String headlongVersion = '13.2.0'
final String junitVersion = '5.11.4'
final DateTimeFormatter dateFormatter = DateTimeFormatter.ofPattern("MMMM d yyyy", Locale.ENGLISH).withZone(ZoneId.of("UTC"))

task fatJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'com.esaulpaugh.headlong.cli.Main',
                   'Implementation-Title': project.name,
                   'Implementation-Version': project.version,
                   'headlong-Version': headlongVersion,
                   'Build-Date': dateFormatter.format(Instant.now())

    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

dependencies {

//    implementation files('libs/headlong-' + headlongVersion + '.jar')

    implementation("com.esaulpaugh:headlong:$headlongVersion") {
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
    }
//    implementation("com.google.code.gson:gson:2.11.0") {
//        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
//    }

    test.useJUnitPlatform()
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}