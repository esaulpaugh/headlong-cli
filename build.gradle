import org.gradle.internal.jvm.Jvm

import java.time.Instant
import java.time.ZoneId
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
}

group 'com.esaulpaugh'
version '0.9-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    if (Jvm.current().getJavaVersion() > JavaVersion.VERSION_1_8) {
        options.compilerArgs.addAll(['--release', '8'])
    }
}

repositories {
    mavenCentral()
//    mavenLocal()
}

final String headlongVersion = '6.3.1'
final String junitVersion = '5.8.2'
final DateTimeFormatter dateFormatter = DateTimeFormatter.ofPattern("MMMM d yyyy", Locale.ENGLISH).withZone(ZoneId.of("UTC"))

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.esaulpaugh.headlong.cli.Main',
                   'Implementation-Title': project.name,
                   'Implementation-Version': project.version,
                   'headlong-version': headlongVersion,
                   'Build-Date': dateFormatter.format(Instant.now())

    }
    archiveName(project.name + '-' + project.version + '.jar') // deprecated
//    archiveFileName(project.name + '-' + project.version + '.jar')
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

dependencies {

//    implementation files('libs/headlong-' + headlongVersion + '.jar')

    implementation 'com.esaulpaugh:headlong:' + headlongVersion
    implementation 'com.google.code.gson:gson:2.9.0'

    test.useJUnitPlatform()
    testImplementation "org.junit.jupiter:junit-jupiter-api:" + junitVersion
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:" + junitVersion
}
