import org.gradle.internal.jvm.Jvm

import java.text.SimpleDateFormat

plugins {
    id 'java'
}

group 'com.esaulpaugh'
version '0.9-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    if (Jvm.current().getJavaVersion().compareTo(JavaVersion.VERSION_1_8) < 0) {
        options.compilerArgs.addAll(['--release', '8'])
    }
}

repositories {
    mavenCentral()
//    mavenLocal()
}

def headlongVersion = '5.4.2'
def junitVersion = '5.8.1'

static def todayUTC() {
    SimpleDateFormat sdf = new SimpleDateFormat("MMMMM d yyyy")
    sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
    return sdf.format(new Date())
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.esaulpaugh.headlong.cli.Main',
                   'Implementation-Title': project.name,
                   'Implementation-Version': project.version,
                   'headlong-version': headlongVersion,
                   'Build-Date': todayUTC()

    }
    archiveName(project.name + '-' + project.version + '.jar') // deprecated
//    archiveFileName(project.name + '-' + project.version + '.jar')
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

dependencies {

//    implementation files('libs/headlong-' + headlongVersion + '.jar')

    implementation 'com.esaulpaugh:headlong:' + headlongVersion
    implementation 'com.google.code.gson:gson:2.8.8'

    test.useJUnitPlatform()
    testImplementation "org.junit.jupiter:junit-jupiter-api:" + junitVersion
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:" + junitVersion
}
